/*
	THIS IS STILL EXPERIMENTAL - IT IS NOT A COMPLETE IMPLEMENTATION
*/

subprojects
{
    apply plugin: 'java'

    group = 'com.netflix.curator'
    version = "1.1.2"

    repositories
    {
        mavenCentral()
        maven
        {
            url "https://repository.jboss.org/nexus/content/groups/public/"
        }
    }

    dependencies 
    {
        compile     'org.apache.zookeeper:zookeeper:3.4.2'
        compile     'com.google.guava:guava:10.0.1'
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources' 
        from sourceSets.main.allSource 
    } 

    task javadocJar(type: Jar, dependsOn:javadoc) { 
        classifier = 'javadoc' 
        from javadoc.destinationDir 
    }
}

task aggregateJavadoc(type: Javadoc) {
    description = 'Aggregate all subproject docs into a single docs directory'
    source subprojects.collect {project -> project.sourceSets.main.allJava } 
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath}) 
    destinationDir = new File(projectDir, 'doc')
}

project(':curator-client')
{
    dependencies
    {
        compile 'org.slf4j:slf4j-api:1.6.3'
        testCompile 'org.mockito:mockito-core:1.8.5'
        testCompile project(':curator-test')
    }
}

project(':curator-test')
{
    dependencies
    {
        compile 'org.javassist:javassist:3.15.0-GA'
        compile 'org.testng:testng:6.1.1'
        compile 'org.apache.commons:commons-math:2.0'
    }
}

project(':curator-framework')
{
    dependencies
    {
        compile project(':curator-client')
        testCompile project(':curator-test')
    }
}

project(':curator-recipes')
{
    dependencies
    {
        compile project(':curator-framework')
        testCompile 'org.mockito:mockito-core:1.8.5'
        testCompile project(':curator-test')
    }
}

project(':curator-x-discovery')
{
    dependencies
    {
        compile project(':curator-framework')
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
        testCompile project(':curator-test')
    }
}

project(':curator-x-discovery-server')
{
    dependencies
    {
        compile project(':curator-x-discovery')
        compile 'javax.ws.rs:jsr311-api:1.1.1'
        testCompile 'junit:junit:4.10'
        testCompile 'com.sun.jersey:jersey-server:1.11'
        testCompile 'com.sun.jersey:jersey-servlet:1.11'
        testCompile 'com.sun.jersey:jersey-client:1.11'
        testCompile 'com.sun.jersey:jersey-core:1.11'
        testCompile 'org.jboss.resteasy:resteasy-jaxrs:2.3.0.GA'
        testCompile 'org.mortbay.jetty:jetty:6.1.9'
    }
}
